apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply from: rootProject.file("dependencies.gradle")

group = 'com.github.kizitonwose'
def artifact = 'CalendarCompose'

android {
    compileSdkVersion versions.compile_sdk
    namespace 'com.kizitonwose.calendarcompose'
    defaultConfig {
        minSdkVersion versions.min_sdk_library
        targetSdkVersion versions.target_sdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true // Because library's minSdk < 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "$group.$artifact"]
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        // See compose/kotlin version mapping https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion '1.3.0'
    }
}

dependencies {
    api project(':core')
    coreLibraryDesugaring deps.desugaring
    implementation deps.kotlin.stdlib8
    implementation deps.androidx.core_ktx
    implementation deps.kotlin.coroutines_core
    implementation deps.kotlin.coroutines_android

    implementation 'androidx.compose.animation:animation:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    implementation 'androidx.compose.ui:ui:1.2.1'
    implementation "androidx.compose.foundation:foundation:1.2.1"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.kizitonwose'
                artifactId = 'CalendarCompose'
                version = '1.1.0' // TODO: Unify versions
            }
        }
    }
}
